buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8"
        classpath 'com.vdurmont:semver4j:3.1.0'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'checkstyle'
    id 'codenarc'
    id 'me.champeau.gradle.jmh' version '0.5.3'
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'net.saliman.cobertura' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'net.ltgt.errorprone' version '1.3.0'
}

apply plugin: 'kotlin'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property 'sonar.java.source', '1.8'
    }
}


group 'com.github.platan'

repositories {
    mavenCentral()
}

ext {
    String ideaVersion = project.hasProperty('ideaVersion') ? project.getProperty('ideaVersion') : ideaVersion
}

intellij {
    version = ideaVersion
    type = 'IC'
    plugins = ['Groovy', 'gradle', 'properties', 'junit'] + javaPlugin()
    downloadSources = false
    updateSinceUntilBuild = false
    instrumentCode = false
}

runPluginVerifier {
    ideVersions = ["IC-2019.3.5",
                   "IC-2020.1.4",
                   "IC-2020.2.4",
                   "IC-2020.3.1"]
}

publishPlugin {
    token = project.hasProperty('publishToken') ? project['publishToken'] : null
    channels = project.hasProperty('publishChannels') ? project['publishChannels'] : ['developer']
}

import com.vdurmont.semver4j.Semver

List<String> javaPlugin() {
    def ideaMainVersion = getIdeaMainVersion()
    ideaVersion == 'LATEST-EAP-SNAPSHOT' || new Semver(ideaMainVersion, Semver.SemverType.LOOSE).isGreaterThanOrEqualTo('2019.2') ? ['java'] : []
}

private String getIdeaMainVersion() {
    String version = ideaVersion
    if (version.startsWith('LATEST'))
        return 'LATEST'
    def versionMatcher
    if ((versionMatcher = version =~ /^(?:(?:IU|UC)-)?(\d+\.\d+)\.\d+/)) {
        return versionMatcher.group(1)
    }
    if ((versionMatcher = version =~ /^(?:(?:IU|UC)-)?(\d+\.\d+)/)) {
        return versionMatcher.group(1)
    }
    throw new GradleException('Cannot parse IntelliJ IDEA version: ' + version)
}

dependencies {
    compile files("src/main/resources")
    compile 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.0'
    compile 'com.sun.xml.bind:jaxb-impl:3.0.0'

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testCompile("org.spockframework:spock-core:1.3-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    // without this dependency I get java.lang.NoClassDefFoundError: kotlin/reflect/jvm/ReflectJvmMapping
    // in StringNotationToMapNotationIntentionTest, MapNotationToStringNotationIntentionTest and SortDependenciesTest
    // workaround from https://baijifeilong.github.io/2018/06/27/kotlin/
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: "$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    if (JavaVersion.current().isJava9Compatible()) {
        errorprone("com.google.errorprone:error_prone_core:2.5.1")
    }
}

configurations {
    all*.exclude group: 'org.gmetrics', module: 'GMetrics'
}

idea {
    module {
        scopes.TEST.plus += [configurations.jmh]
    }
}

codenarc {
    configFile = file("${project.rootDir}/config/codenarc/rules.groovy")
    toolVersion = codenarcToolVersion
}

cobertura.coverageFormats = ['html', 'xml']

checkstyle {
    toolVersion = checkstyleToolVersion
    configFile = file("${project.rootDir}/config/checkstyle/checks.xml")
}

test {
    jvmArgs '-XX:MaxPermSize=256m'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
